load("@rules_jvm_external//:defs.bzl", "artifact")
load("//:copy_file.bzl", "copy_file")

exports_files([
    "htmlutils.js",
    "injectableSelenium.js",
])

java_library(
    name = "webdriven",
    srcs = [
        "CompoundMutator.java",
        "ExplodingSupplier.java",
        "FunctionDeclaration.java",
        "SeleniumMutator.java",
        "VariableDeclaration.java",
        "WebDriverBackedSelenium.java",
        "WebDriverCommandProcessor.java",
    ],
    resources = [
        "htmlutils.js",
        "injectableSelenium.js",
        ":findElement",
        ":findOption",
        ":fireEvent",
        ":fireEventAt",
        ":getAttribute",
        ":getText",
        ":linkLocator",
        ":isElementPresent",
        ":isSomethingSelected",
        ":isTextPresent",
        ":isVisible",
        ":setCursorPosition",
        ":type",
        # TODO(simons): remove sizzle. There are no longer any browsers that need it.
        ":sizzle",
    ],
    visibility = [
        "//java/client/src/com/smartqa/automationagent:__subpackages__",
        "//java/client/test/com/smartqa/automationagent:__subpackages__",
        "//java/server/src/com/smartqa/automationagent/webdriven:__pkg__",
        "//java/server/src/org/smartqa/grid/automationagent:__pkg__",
        "//java/server/src/org/smartqa/automationagent/server/htmlrunner:__pkg__",
    ],
    exports = [
        ":emulation-api",
        "//java/client/src/com/smartqa/automationagent/webdriven/commands",
    ],
    deps = [
        ":emulation-api",
        "//java/client/src/com/smartqa/automationagent",
        "//java/client/src/com/smartqa/automationagent/webdriven/commands",
        "//java/client/src/org/smartqa/automationagent:core",
        "//java/client/src/org/smartqa/automationagent/remote",
        artifact("com.google.guava:guava"),
    ],
)

java_library(
    name = "emulation-api",
    srcs = [
        "ElementFinder.java",
        "JavascriptLibrary.java",
        "ScriptMutator.java",
        "SeleneseCommand.java",
        "Timer.java",
        "Windows.java",
    ],
    visibility = [
        "//java/client/src/com/smartqa/automationagent:__subpackages__",
        "//java/client/test/com/smartqa/automationagent:__subpackages__",
    ],
    deps = [
        "//java/client/src/com/smartqa/automationagent",
        "//java/client/src/org/smartqa/automationagent:core",
        artifact("com.google.guava:guava"),
    ],
)

[copy_file(
    name = name,
    src = "//javascript/automationAgent-atoms:%s.js" % name,
    out = "%s.js" % name,
) for name in [
    "findElement",
    "findOption",
    "fireEvent",
    "fireEventAt",
    "getAttribute",
    "getText",
    "linkLocator",
    "isElementPresent",
    "isSomethingSelected",
    "isTextPresent",
    "isVisible",
    "setCursorPosition",
    "type",
]]

copy_file(
    name = "sizzle",
    src = "//third_party/js/sizzle",
    out = "sizzle.js",
)
